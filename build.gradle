plugins {
    id 'java'
    id 'application'
    id 'org.beryx.runtime' version '1.13.0'
}

version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    implementation 'com.itextpdf:itextpdf:5.5.13.3'
}

test {
    useJUnit()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

sourceCompatibility = '17'
targetCompatibility = '17'

application {
    mainClass = 'PDFSignerApp'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/main/java']
        }
    }
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    
    modules = ['java.base', 'java.desktop']
    
    launcher {
        noConsole = true
    }
    
    jpackage {
        // Platform specific options
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            imageOptions = ['--win-console']
            installerOptions = [
                '--win-per-user-install',
                '--win-dir-chooser',
                '--win-shortcut',
                '--win-menu'
            ]
        } else if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            imageOptions = ['--mac-package-name', 'PDFSigner']
            installerOptions = [
                '--mac-package-identifier', 'com.pdfsigner',
                '--mac-package-name', 'PDFSigner',
                '--mac-package-signing-prefix', 'Developer ID Application'
            ]
        } else {
            imageOptions = ['--linux-shortcut']
            installerOptions = [
                '--linux-menu-group', 'Office',
                '--linux-shortcut'
            ]
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'PDFSignerApp'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Tarea para copiar el JAR al directorio releases
task copyJarToReleases(type: Copy) {
    dependsOn jar
    from "${buildDir}/libs"
    into "releases"
    include "*.jar"
    rename { String fileName ->
        "PDFSigner-${project.version}.jar"
    }
}

// Tarea para copiar las dependencias a releases/libs
task copyDependenciesToReleases(type: Copy) {
    from configurations.runtimeClasspath
    into "releases/libs"
}

// Hacer que la tarea build dependa de ambas tareas
build.dependsOn copyJarToReleases, copyDependenciesToReleases 